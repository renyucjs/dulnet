# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# the os value could be:
# - "android"
# - "chromeos"
# - "ios"
# - "linux"
# - "nacl"
# - "mac"
# - "win"
if (target_os == "") {
  target_os = host_os
}
if (current_os == "") {
  current_os = target_os
}

# the value could be:
#   - "x64"
#   - "x86"
#   - "ssd201"
if (target_cpu == "") {
  target_cpu = host_cpu
}
if (current_cpu == "") {
  current_cpu = target_cpu
}


declare_args() {
  is_debug = false
  has_alsa = false
}

is_linux = host_os == "linux" && current_os == "linux" && target_os == "linux"
is_mac = host_os == "mac" && current_os == "mac" && target_os == "mac"
is_win = host_os=="win" && current_os == "win" && target_os == "win"

# All binary targets will get this list of configs by default.
_shared_binary_target_configs = [ "//build:compiler_defaults" ]

# Add os_cpu_defines symbols
_shared_binary_target_configs += [ "//build:os_cpu_defines" ]

#Add debug symbols
if(is_debug){
  _shared_binary_target_configs += [ "//build:debug_symbols" ]
  _shared_binary_target_configs += [ "//build:debug_defines" ]
}
# Apply that default list to the binary target types.
set_defaults("executable") {
  configs = _shared_binary_target_configs

  # Executables get this additional configuration.
  configs += [ "//build:executable_ldconfig" ]
}
set_defaults("static_library") {
  configs = _shared_binary_target_configs
}
set_defaults("shared_library") {
  configs = _shared_binary_target_configs
}
set_defaults("source_set") {
  configs = _shared_binary_target_configs
}

set_default_toolchain("//build/toolchain:default-gcc")
